
plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.15.0"
}

ext {
    springBootVersion = '1.5.6.RELEASE'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    testImplementation 'junit:junit:4.12'
    testImplementation("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    testImplementation gradleTestKit()
    testImplementation("org.spockframework:spock-core:${spockVersion}"){
            exclude group: 'org.codehaus.groovy'
    }
}

group = 'com.garyclayburg'
version = '1.4.2-SNAPSHOT'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                url = 'https://github.com/gclayburg/dockerPreparePlugin'
                name = 'docker prepare'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'gclayburg'
                        name = 'Gary Clayburg'
                        email = '@garyclayburg'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/gclayburg/dockerPreparePlugin.git'
                    developerConnection = 'scm:git:https://github.com/gclayburg/dockerPreparePlugin.git'
                    url = 'https://github.com/gclayburg/dockerPreparePlugin'
                }

            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('../repo')
        }
    }
}

/*   (old 'maven' plugin)
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('../repo'))
        }
    }
}
*/

//gradlew publishPlugins
pluginBundle {
    website = 'https://github.com/gclayburg/dockerPreparePlugin'
    vcsUrl = 'https://github.com/gclayburg/dockerPreparePlugin'
    description = 'Gradle plugin to generate docker layer-friendly directory for spring boot applications'
    tags = ['docker', 'groovy', 'spring-boot']
}
gradlePlugin {
    plugins {
        dockerprepare {
            id = 'com.garyclayburg.dockerprepare'
            displayName = 'Prepare docker layer-friendly directory'
            description = 'Gradle plugin to generate docker layer-friendly directory for spring boot applications'
            implementationClass = 'com.garyclayburg.docker.DockerPreparePlugin'
        }
    }
}

/*
For full testing, we need to execute with older and newer gradle:

$ sdk use gradle 4.10.3 && gradle --version && MODERN_GRADLE=false gradle clean build
$ sdk use gradle 6.8.3 && gradle --version && MODERN_GRADLE=true gradle clean build

To publish to 'repo' directory:
$ ./gradlew publish --info -x test

 */
